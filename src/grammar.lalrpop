use std::str::FromStr;
use crate::ast::{Program, Header, Expr, Opcode};

use lalrpop_util::ParseError;

grammar;

pub Program: Box<Program> = {
    <h: Header> => Box::new(Program{ header: h }),
};

pub Header: Header = {
    "program" <Id> => Header::Identifier(<>.to_string()),
};

pub Expr: Box<Expr> = {
    AddExpr RelOp AddExpr => Box::new(Expr::Op(<>)),
    AddExpr,
};

RelOp: Opcode = {
    "=" => Opcode::Eq,
    "<" => Opcode::Lt,
    ">" => Opcode::Gt,
    "<=" => Opcode::Le,
    ">=" => Opcode::Ge,
    "<>" => Opcode::Neq,
}

AddExpr: Box<Expr> = {
    AddExpr AddOp MulExpr => Box::new(Expr::Op(<>)),
    MulExpr,
}

AddOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
    "or" => Opcode::Or,
};

MulExpr: Box<Expr> = {
    MulExpr MulOp Term => Box::new(Expr::Op(<>)),
    Term,
};

MulOp: Opcode= {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
    "%" => Opcode::Mod,
    "and" => Opcode::And,
};

Term: Box<Expr> = {
    Integer => Box::new(Expr::Integer(<>)),
    Double => Box::new(Expr::Double(<>)),
    String => Box::new(Expr::String(<>)),
    Boolean => Box::new(Expr::Boolean(<>)),
    Identifier => Box::new(Expr::Identifier(<>)),
    "(" <Expr> ")",
};

Integer: i32 = {
    r"[0-9]+" =>? i32::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number is too big"
        }),
};

Double: f64 = {
    r"[0-9]+\.[0-9]+" =>? f64::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number is too big"
        }),
};

String: String = {
    r"'[^']*'" => <>.to_string(),
};

Boolean: bool = {
    "true" => true,
    "false" => false,
}

Identifier: String = {
    Id => <>.to_string(),
};

match {
    // String match
    // TODO: Determine terminals should be included here or if they should be included using '_' in low prio
    "program",

    // Operators
    // Arithmetic
    "+", "-", "*", "/", "%",
    // Comparison
    "=", "<", ">", "<=", ">=", "<>",
    // Boolean
    "and", "or", "not",

    "(", ")",
    "true", "false",

    // Skip
    r"\s*" => { }, // Whitespace
    r"//[^\n\r]*[\n\r]*" => { }, // Comments
    r"\(\*[\s\S]*\*\)" => { },
    r"\{[\s\S]*\}" => { },
} else {
    // Low prio
    // TODO: Determine if numbers belong here
    r"[0-9]+",
    r"[0-9]+\.[0-9]+",
    r"'[^']*'",
    r"[a-zA-Z_][a-zA-Z0-9_]*" => Id,
}
