use std::str::FromStr;
use crate::ast::{Program, Header, Expr, BinaryOp, UnaryOp};

use lalrpop_util::ParseError;

grammar;

pub Program: Box<Program> = {
    <h: Header> => Box::new(Program{ header: h }),
};

pub Header: Header = {
    "program" <Id> => Header::Identifier(<>.to_string()),
};

pub Expr: Box<Expr> = {
    AddExpr RelOp AddExpr => Box::new(Expr::Binary(<>)),
    AddExpr,
};

RelOp: BinaryOp = {
    "=" => BinaryOp::Eq,
    "<" => BinaryOp::Lt,
    ">" => BinaryOp::Gt,
    "<=" => BinaryOp::Le,
    ">=" => BinaryOp::Ge,
    "<>" => BinaryOp::Neq,
}

AddExpr: Box<Expr> = {
    AddExpr AddOp MulExpr => Box::new(Expr::Binary(<>)),
    MulExpr,
}

AddOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
    "or" => BinaryOp::Or,
};

MulExpr: Box<Expr> = {
    MulExpr MulOp UnaryExpr => Box::new(Expr::Binary(<>)),
    UnaryExpr,
};

MulOp: BinaryOp= {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
    "%" => BinaryOp::Mod,
    "and" => BinaryOp::And,
};

pub UnaryExpr: Box<Expr> = {
    UnaryOp UnaryExpr => Box::new(Expr::Unary(<>)),
    Term,
};

UnaryOp: UnaryOp = {
    "+" => UnaryOp::Pos,
    "-" => UnaryOp::Neg,
    "not" => UnaryOp::Not,
};

pub Term: Box<Expr> = {
    Integer => Box::new(Expr::Integer(<>)),
    Double => Box::new(Expr::Double(<>)),
    String => Box::new(Expr::String(<>)),
    Boolean => Box::new(Expr::Boolean(<>)),
    Identifier => Box::new(Expr::Identifier(<>)),
    "(" <Expr> ")",
};

Integer: i32 = {
    r"[0-9]+" =>? i32::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number is too big"
        }),
};

Double: f64 = {
    r"[0-9]+\.[0-9]+" =>? f64::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number is too big"
        }),
};

pub String: String = {
    r"'[^']*'" => <>.to_string(),
};

Boolean: bool = {
    "true" => true,
    "false" => false,
}

Identifier: String = {
    Id => <>.to_string(),
};

match {
    // String match
    // TODO: Determine terminals should be included here or if they should be included using '_' in low prio
    "program",

    // Operators
    // Arithmetic
    "+", "-", "*", "/", "%",
    // Comparison
    "=", "<", ">", "<=", ">=", "<>",
    // Boolean
    "and", "or", "not",

    "(", ")",
    "true", "false",

    // Skip
    r"\s*" => { }, // Whitespace
    r"//[^\n\r]*[\n\r]*" => { }, // Comments
    r"\(\*[\s\S]*\*\)" => { },
    r"\{[^}]*\}" => { },
} else {
    // Low prio
    // TODO: Determine if numbers belong here
    r"[0-9]+",
    r"[0-9]+\.[0-9]+",
    r"'[^']*'",
    r"[a-zA-Z_][a-zA-Z0-9_]*" => Id,
}
